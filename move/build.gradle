buildscript {
    ext {
        set('javaVersion', "17");
        set('springBootVersion', "2.7.17"); // 3.1.5
        set('dependencyManagementVersion', "1.0.15.RELEASE"); // 1.1.3

        set('springCloudVersion', "2021.0.8") // 2022.0.4
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'kim.zhyun'
    sourceCompatibility = "${javaVersion}"
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('bootBuildImage') {
        builder = 'paketobuildpacks/builder-jammy-base:latest'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

project(":config") {
    bootJar { enabled = true }
    jar { enabled = false}

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-config-server'
    }
}

project(":velog") {
    bootJar { enabled = true }
    jar { enabled = false}

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'
    }
}

project(":tistory") {
    bootJar { enabled = true }
    jar { enabled = false}

    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'

        implementation 'commons-io:commons-io:2.11.0'
        implementation 'commons-fileupload:commons-fileupload:1.4'
    }
}
